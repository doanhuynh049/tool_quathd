getdir DIR

;===================================================
;; COMポート／ボー・レート設定
COM_PORT = '4'
BAUDRATE = '115200'
;===================================================
;; ①コマンド組立て
COMMAND = '/C='
strconcat COMMAND COM_PORT
strconcat COMMAND ' /BAUD='
strconcat COMMAND BAUDRATE

;; ②接続
;connect COMMAND

setbaud 115200

;---------------------------------------------------
; Initialize DRAM
;---------------------------------------------------
;wait '>'
;mpause 100
;sendln 'init_dram'

;---------------------------------------------------
; Setting load image parameter
;---------------------------------------------------
ARRAY_NUM = 6
strdim IMGNM ARRAY_NUM
IMGNM[0] = 'bootparam_sa0-4x2g.srec'
IMGNM[1] = 'bl2-salvator-x-4x2g.srec'
IMGNM[2] = 'cert_header_sa6-4x2g.srec'
IMGNM[3] = 'bl31-salvator-x-4x2g.srec'
IMGNM[4] = 'tee-salvator-x.srec'
IMGNM[5] = 'u-boot-elf-salvator-x.srec'

;Automatically identify the start address from S-record.
strdim IMGADR ARRAY_NUM
IMGADR[0] = 'E6320000'
IMGADR[1] = 'E6304000'
IMGADR[2] = 'E6320000'
IMGADR[3] = '44000000'
IMGADR[4] = '44100000'
IMGADR[5] = '50000000'

strdim FLASHADR ARRAY_NUM
FLASHADR[0] = '0'
FLASHADR[1] = '40000'
FLASHADR[2] = '180000'
FLASHADR[3] = '1C0000'
FLASHADR[4] = '200000'
FLASHADR[5] = '640000'

;--------------------------------------------------------------------------
; switch Scif speed UP (Change to 460.8kbps or 921.6kbps baud rate setting)
;--------------------------------------------------------------------------
wait '>'
mpause 100

sendln 'sup'
wait 'Change to 460.8Kbps baud rate setting of the SCIF. OK? (y/n)' 'Please change to 921.6Kbps baud rate setting of the terminal.' 'Please change to 460.8Kbps baud rate setting of the terminal.'

If result=1 then
  send 'y'
  wait 'Please change to 460.8Kbps baud rate setting of the terminal.'
  setbaud 460800
elseif result=2 then
  ; result=2
  setbaud 921600
else
  ; result=3
  setbaud 460800
endif



;---------------------------------------------------
; Select load all images or selected image
;---------------------------------------------------
;wait '>'
;mpause 100

yesnobox 'Load all images?' 'Image Selection'
Str_Yesno = result

if Str_Yesno=1 then
  call Load_all_image
else
  call Load_selected_image
endif

setbaud 115200

end

;---------------------------------------------------
; load all images
;---------------------------------------------------
:Load_All_image

; file check
for i 0 ARRAY_NUM-1
    filesearch IMGNM[i]
    if result=0 then
        sprintf 'File "%s" not found' IMGNM[i]
        messagebox inputstr 'error'
        break
    endif
next

if result=1 then
    for i 0 ARRAY_NUM-1
        index = i
        call Decode_srec_start_address
        call Load_Image
    next
else
    messagebox "Cancel write all images" "error"
endif

;messagebox "全焼きこみ終了" ""
return

;---------------------------------------------------
; Select load selected Image
;---------------------------------------------------
:Load_selected_image
:retry
    listbox 'Please select an image to load' 'Image Selection' IMGNM
    index = result
    if index = -1 then
;        messagebox "書き込み終了" ""
    else
        ; file check
        filesearch IMGNM[index]
        If result=0 then
            sprintf 'File "%s" not found' IMGNM[index]
            messagebox inputstr 'error'
        else
            call Decode_srec_start_address
            call Load_Image
        endif
        goto retry
    endif
return

;---------------------------------------------------
; Decoed S-record start address
;---------------------------------------------------
:Decode_srec_start_address
    fileopen fhandle IMGNM[index] 0 1
    if fhandle = -1 then
        sprintf 'File "%s" open error' IMGNM[i]
        messagebox inputstr 'error'
        break
    endif
    filereadln fhandle strvar
    if result=1 then
        fileclose fhandle
        sprintf 'File "%s" S-record format error' IMGNM[i]
        messagebox inputstr 'error'
        break
    endif
    filereadln fhandle strvar
    strcopy strvar 5 8 IMGADR[index]
    fileclose fhandle
return

;---------------------------------------------------
; Load Image
;---------------------------------------------------
:Load_Image
mpause 20
sendln 'xls2'
wait '  Select (1-3)>'
mpause 20
sendln '3'
wait 'SW1 SW2 All OFF!  Setting OK? (Push Y key)'  'SW1 SW2 All OFF!   Setting OK? (Push Y key)'
mpause 20
send 'y'
wait 'SW3 ON!  Setting OK? (Push Y key)'  'SW3 ON!            Setting OK? (Push Y key)'
mpause 20
send 'y'

wait 'SW31 ON!  Setting OK? (Push Y key) [In the case of Kriek Board]'  '  Please Input : H'
If result=1 then
  mpause 20
  send 'y'
  wait '  Please Input : H'
endif

mpause 1
sendln IMGADR[index]
wait '  Please Input : H'
sendln FLASHADR[index]
wait 'CR stop load)'
mpause 400
FILE_NAME = DIR
strconcat FILE_NAME '\'
strconcat FILE_NAME IMGNM[index]
sendfile FILE_NAME 1
wait 'complete!' 'Clear OK?(y/n)'
If result=1 goto proc_end
If result=2 goto send_y

:send_y
send 'y'
wait '>'

:proc_end
mpause 100
return

;; ③マクロ終了
setbaud 115200
;end

